<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.3">
  <POU Name="exFIX" Id="{9d53a6b2-7967-4db1-8166-fb2a2392199f}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION exFIX : STRING(STRING_LENGTH)
VAR_INPUT
	str: STRING(STRING_LENGTH);
	L: INT;
	C: BYTE;
	M: INT;
END_VAR
VAR
	N: INT;
	SX: STRING(STRING_LENGTH);
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* make sure L does not exceed the limits of a string *)
N := LIMIT(0,L,STRING_LENGTH) - LEN(str);
IF N <= 0 THEN
	(* truncate the string at length N *)
	IF M = 1 THEN
		exFIX := RIGHT(str,L);
	ELSE
		exFIX := LEFT(str,L);
	END_IF;
ELSIF M = 1 THEN
	(* connect fill characters at the beginning *)
	sx := exFILL(C,N);
	exFIX := CONCAT(sx,str);
ELSIF M = 2 THEN
	(* center str beween fill characters *)
	(* for an uneven number of fill characters, there is one more at the end *)
	sx := exFILL(C,SHR(N+1,1));
	exFIX := CONCAT(str,sx);
	SX := LEFT(sx,SHR(N,1));
	exFIX := CONCAT(sx,exFIX);
ELSE
	(* connect fill characters at the end *)
	SX := exFILL(C,N);
	exFIX := CONCAT(str,SX);
END_IF;


(* revision histroy
hm	29. mar. 2008	rev 1.0
	original release

*)]]></ST>
    </Implementation>
    <LineIds Name="exFIX">
      <LineId Id="3" Count="30" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>